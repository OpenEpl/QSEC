.程序集 快速文本连接, <对象>
.程序集变量 缓冲区地址, 整数型
.程序集变量 缓冲区长度, 整数型
.程序集变量 内容长度, 整数型

.子程序 _初始化, , , 当基于本类的对象被创建后，此方法会被自动调用

缓冲区长度 = 16
缓冲区地址 = HeapAlloc (GetProcessHeap (), 0, 缓冲区长度)
.子程序 _销毁, , , 当基于本类的对象被销毁前，此方法会被自动调用

HeapFree (GetProcessHeap (), 0, 缓冲区地址)
.子程序 扩展
.参数 扩展后最小大小, 整数型
.局部变量 两倍, 整数型
.局部变量 原缓冲区地址, 整数型

.如果真 (扩展后最小大小 <= 缓冲区长度)
    返回 ()
.如果真结束

原缓冲区地址 = 缓冲区地址

两倍 = 缓冲区长度 * 2

.如果 (两倍 < 扩展后最小大小)
    缓冲区长度 = 扩展后最小大小
.否则
    缓冲区长度 = 两倍
.如果结束

缓冲区地址 = HeapAlloc (GetProcessHeap (), 0, 缓冲区长度)
.如果真 (缓冲区地址 == 0)
    缓冲区地址 = HeapAlloc (GetProcessHeap (), #HEAP_GENERATE_EXCEPTIONS, 扩展后最小大小)
.如果真结束
.如果真 (内容长度 != 0)
    CopyMemory (缓冲区地址, 原缓冲区地址, 内容长度)
.如果真结束

HeapFree (GetProcessHeap (), 0, 原缓冲区地址)
.子程序 添加, , 公开
.参数 text, 文本型
.局部变量 len, 整数型
.局部变量 pchar, 整数型
.局部变量 新内容长度, 整数型

len = 取文本长度 (text)
pchar = 取变量数据地址_文本型 (text)
新内容长度 = 内容长度 + len
扩展 (新内容长度)
CopyMemory (缓冲区地址 + 内容长度, pchar, len)
内容长度 = 新内容长度
.子程序 取缓冲区地址, 整数型, 公开

返回 (缓冲区地址)
.子程序 取内容长度, 整数型, 公开

返回 (内容长度)
.子程序 清空, , 公开

HeapFree (GetProcessHeap (), 0, 缓冲区地址)
内容长度 = 0
缓冲区长度 = 16
缓冲区地址 = HeapAlloc (GetProcessHeap (), 0, 缓冲区长度)
.子程序 取文本, 文本型, 公开
.局部变量 text, 文本型

text = 取空白文本 (内容长度)
CopyMemory (取变量数据地址_文本型 (text), 缓冲区地址, 内容长度)
返回 (text)