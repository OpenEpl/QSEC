.程序集 整数序列, 序列基类, 公开
.程序集变量 数据, 整数型, , "0"

.子程序 _初始化, , , 当基于本类的对象被创建后，此方法会被自动调用


.子程序 _销毁, , , 当基于本类的对象被销毁前，此方法会被自动调用


.子程序 创建, , 公开
.参数 n, 整数型

重定义数组 (数据, 假, n)
.子程序 创建兼容序列, 序列基类, 公开
.参数 n, 整数型
.局部变量 array, 整数序列

' 注：易语言不支持 协变返回类型 式重写
' 即不能将该方法的返回值类型修改为 序列基类 的子类 整数序列

array.创建 (n)
返回 (array)
.子程序 改变容量, , 公开
.参数 n, 整数型

重定义数组 (数据, 真, n)
.子程序 数据交换, , 公开
.参数 外部数据, 通用型, 参考 数组

asm_交换变量_数组 (数据, 外部数据)
.子程序 取长度, 整数型, 公开, GetLength

返回 (取数组下标 (数据, ))
.子程序 交换成员, , 公开, ExchangeMember
.参数 i, 整数型
.参数 j, 整数型

交换变量 (数据[i], 数据[j])
.子程序 比较成员, 整数型, 公开, CompareMember
.参数 i, 整数型
.参数 j, 整数型

返回 (数据[i] - 数据[j])
.子程序 比较成员_与另一序列, 整数型, 公开, CompareMember_WithMemberInOtherArray
.参数 i, 整数型
.参数 另一序列, 序列基类
.参数 j, 整数型
.局部变量 op2, 整数型

op2 = 指针到整数 (另一序列.取成员_指针 (j))
返回 (数据[i] - op2)
.子程序 比较成员_与外部对象, 整数型, 公开
.参数 i, 整数型
.参数 外部对象, 通用型, 参考

返回 (数据[i] - 读通用型_整数 (外部对象))
.子程序 调试, , 公开
.局部变量 i, 整数型
.局部变量 t, 文本型

t = “整数序列 - 长度：” + 到文本 (取数组下标 (数据, ))
.如果真 (取数组下标 (数据, ) > 0)
    t = t + “   { ”
    t = t + 到文本 (数据[1])
    .变量循环首 (2, 取数组下标 (数据, ), 1, i)
        t = t + “, ”
        t = t + 到文本 (数据[i])
    .变量循环尾 ()
    t = t + “ }”
.如果真结束
输出调试文本 (t)
.子程序 取成员_指针, 整数型, 公开
.参数 i, 整数型

返回 (取变量地址 (数据[i]))

.子程序 取成员_大小, 整数型, 公开, 对复杂数据，返回4（数据指针大小）

返回 (4)
.子程序 置成员, , 公开
.参数 i, 整数型
.参数 newValue, 通用型, 参考, 自行保证数据类型符合

数据[i] = 读通用型_整数 (newValue)
.子程序 取成员, , 公开
.参数 i, 整数型
.参数 out_value, 通用型, 参考, 自行保证数据类型符合

写通用型_整数 (out_value, 数据[i])